plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "com.github.blueboxware.tocme" version "1.2" // For TOC in README
}

group 'de.unruh'
version 'master-SNAPSHOT'

// TODO: Make sure we compile for a reasonably old JVM version

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:19.0.0'
    testImplementation 'org.jetbrains:annotations:19.0.0'
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.0"
}

java {
    withJavadocJar()
    withSourcesJar()

}

// TODO: Can we go for an earlier version (at least <=8) if we add type annotations everywhere (keep tests Java 9)
compileJava {
    options.release = 8
}

compileTestJava {
    options.release = 9
}

test {
    useJUnitPlatform()
}

tocme {
    doc(file("README.md"))
}

/* Put this into ~/.gradle/gradle.properties for publishing to work:

signing.keyId=4BA66FE2
signing.password=******************
signing.secretKeyRingFile=/home/unruh/.gnupg/secring.gpg

ossrhUsername=unruh
ossrhPassword=******************

*/

// Potentially useful: https://sasbury.github.io/notes/2019/gradle_travis_sonatype.html

publishing {
    publications {
       javaPatterns(MavenPublication) {
           from components.java
       }
    }

    repositories {
        maven {
            name = 'sonatype-snapshot'
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
            credentials {
                username "unruh"
                password project.properties.ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.javaPatterns
}
